# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos_var.gni")

import("//foundation/resourceschedule/plugins/memmgr/memmgr.gni")

## Install memmgrservice.rc to /system/etc/init/memmgrservice.rc {{{
ohos_prebuilt_etc("memmgrservice_init") {
  if (use_musl) {
    source = "memmgrservice.cfg"
  } else {
    source = "memmgrservice.rc"
  }
  relative_install_dir = "init"
  part_name = "${memmgr_part_name}"
  subsystem_name = "${memmgr_subsystem_name}"
}
## Install memmgrservice.rc to /system/etc/init/memmgrservice.rc }}}

config("memory_memmgr_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "${memmgr_root_path}/common/include",
    "include/event/",
    "include/reclaim_priority_manager/",
    "include/kill_strategy_manager/",
    "include/reclaim_strategy_manager/",
    "include/config/",
    "//base/notification/ces_standard/frameworks/core/include/",
    "//base/notification/ces_standard/interfaces/innerkits/native/include",
    "//base/account/os_account/interfaces/innerkits/osaccount/native/include",
    "//base/account/os_account/frameworks/common/database/include",
    "//base/account/os_account/frameworks/common/log/include",
    "//base/account/os_account/frameworks/common/account_error/include"
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AppAccountFwk\"",
  ]
}

ohos_shared_library("memmgrservice") {
  install_enable = true
  sources = [
    "src/mem_mgr_service.cpp",
    "src/event/mem_mgr_event_center.cpp",
    "src/event/app_state_callback_mem_host.cpp",
    "src/event/mem_mgr_event_subscriber.cpp",
    "src/event/mem_mgr_event_observer.cpp",
    "src/event/account_observer.cpp",
    "src/event/account_subscriber.cpp",
    "src/reclaim_priority_manager/reclaim_priority_manager.cpp",
    "src/reclaim_priority_manager/process_priority_info.cpp",
    "src/reclaim_priority_manager/bundle_priority_info.cpp",
    "src/reclaim_priority_manager/os_account_priority_info.cpp",
    "${memmgr_common_path}/src/kernel_interface.cpp",
  ]

  configs = [ ":memory_memmgr_config" ]

  deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//base/startup/syspara_lite/interfaces/innerkits/native/syspara:syspara",
    "//foundation/appexecfwk/standard/interfaces/innerkits/libeventhandler:libeventhandler",
    "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_base:appexecfwk_base",
    "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_core:appexecfwk_core",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata:distributeddata_inner",
    "//foundation/distributedschedule/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//utils/native/base:utils",
    "//base/account/os_account/frameworks/common:libaccount_common",
    "//base/account/os_account/frameworks/osaccount/native:os_account_innerkits",
  ]

  external_deps = [
    "dsoftbus_standard:softbus_client",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "aafwk_standard:want",
    "ces_standard:cesfwk_core",
    "ces_standard:cesfwk_innerkits",
    "hisysevent_native:libhisysevent",
    "os_account_standard:libaccountkits",
  ]

  part_name = "${memmgr_part_name}"
  subsystem_name = "${memmgr_subsystem_name}"
}

group("unittest") {
  testonly = true
  deps = [ "test:unittest" ]
}
